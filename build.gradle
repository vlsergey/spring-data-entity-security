plugins {
	id 'java-library'
	id 'maven-publish'
	id 'net.researchgate.release' version '2.8.1'
}

group 'io.github.vlsergey'

ext {
	lombokVersion = '1.18.12'
	queryDslVersion = '4.4.0'
	springDataVersion = '2.5.4'
}

compileJava {
	options.compilerArgs << '-parameters'
	options.debugOptions.debugLevel = "source,lines,vars"
	options.encoding = 'UTF-8'
	options.release = 11
}

compileTestJava {
	options.compilerArgs << '-parameters'
	options.debugOptions.debugLevel = "source,lines,vars"
	options.encoding = 'UTF-8'
	options.release = 11
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion

	api group: 'org.springframework.data', name: 'spring-data-jpa', version: springDataVersion
	implementation group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'
	implementation group: 'com.querydsl', name: 'querydsl-jpa', version: queryDslVersion

	testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	testAnnotationProcessor("com.querydsl:querydsl-apt:${queryDslVersion}:jpa")
	testAnnotationProcessor group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

	testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
	testImplementation group: 'com.github.gavlyukovskiy', name: 'datasource-decorator-spring-boot-autoconfigure', version: '1.7.1'
	testImplementation group: 'com.querydsl', name: 'querydsl-jpa', version: queryDslVersion
	testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
	testImplementation(platform('org.junit:junit-bom:5.7.2'))
	testImplementation group: 'org.hibernate', name: 'hibernate-core', version: '5.5.6.Final'
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.5.4'
	testImplementation group: 'org.springframework.security', name: 'spring-security-core', version: '5.5.2'
	testImplementation group: 'net.ttddyy', name: 'datasource-proxy', version: '1.7'
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

task regenerateReadme {
	doLast {
		def releaseVersion = project.property('release.releaseVersion');
		copy {
			from file( 'README.template.md')
			rename 'README\\.template\\.md', 'README.md'
			into project.projectDir
			expand([ "version": releaseVersion ])
		}

		exec {
			workingDir project.projectDir
			commandLine 'git'
			args 'add', 'README.md'
		}

		exec {
			workingDir project.projectDir
			commandLine 'git'
			args 'commit', '-m', 'Update version in README.md to ' + releaseVersion
		}
	}
}
beforeReleaseBuild.dependsOn regenerateReadme

release {
	failOnCommitNeeded = true
	failOnSnapshotDependencies = true
	failOnUnversionedFiles = true
	failOnUpdateNeeded = true
	pushReleaseVersionBranch = false
}

publishing {
	publications {
		myLibrary(MavenPublication) {
			from components.java
		}
	}

	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/vlsergey/spring-data-entity-security"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}
// afterReleaseBuild.dependsOn publishPlugins
